SELECT 
  ?person 
  ?personLabel 
  (MIN(?dob) as ?dateOfBirth) 
  (SAMPLE(?cityOfBirthName) as ?cityOfBirthLabel) 
  (MIN(?dod) as ?dateOfDeath) 
  (SAMPLE(?codLabel) as ?causeOfDeathLabel) 
WHERE {
  VALUES ?person { wd:Q11903 wd:Q720 wd:Q762 wd:Q467650 wd:Q171411 wd:Q7186 wd:Q7241 wd:Q8605 wd:Q102870 wd:Q937 }
  
  OPTIONAL { ?person wdt:P569 ?dob. }
  OPTIONAL { ?person wdt:P19 ?cityOfBirth. 
             ?cityOfBirth rdfs:label ?cityOfBirthName FILTER(LANG(?cityOfBirthName) = "en") }
  OPTIONAL { ?person wdt:P570 ?dod. }
  OPTIONAL { ?person wdt:P509 ?cod. 
             ?cod rdfs:label ?codLabel FILTER(LANG(?codLabel) = "en") }
  
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}
GROUP BY ?person ?personLabel
